archivesBaseName = "${rootProject.archive_base_name}-fabric"
version = "${project.mod_version}-${rootProject.minecraft_version}"

apply plugin: "com.github.johnrengelman.shadow"

def generatedResources = file("src/generated/resources")
def accessWidenerFile = project(":common").file("src/main/resources/gtrubber.accesswidener")

loom {
    accessWidenerPath = accessWidenerFile
}

architectury {
    platformSetupLoomIde()
    fabric()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    common(project(path: ":common", configuration: "namedElements"))
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric"))
    modImplementation("io.github.gregtechintergalactical:antimatter-fabric:${antimatter_version}"){
        exclude(group: "curse.maven")
        exclude(group: "maven.modrinth")
    }
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}"
}

sourceSets {
    main {
        resources {
            srcDir generatedResources
        }
    }
}

task copyAccessWidener(type: Copy) {
    from accessWidenerFile
    into generatedResources
}

processResources {
    dependsOn copyAccessWidener

    inputs.property "version", project.mod_version

    filesMatching("fabric.mod.json") {
        expand "version": project.mod_version
    }
}

sourcesJar {
    from(project(':common').sourceSets.main.allSource) {
        exclude("gtrubber.accesswidener") // duplicate on fabric, unneeded on forge
    }
}